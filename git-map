#!/usr/bin/env fish

set _version "v0.1.1"

function map -a "format_str" "file" "command"
    set -l command_args $argv[4..-1]
    set -l revs (git log "--pretty=format:%H" $file)

    set -l i 1
    for line in (git log "--pretty=format:$format_str" $file)
        set -l output (git show $revs[$i]:$file | eval $command $command_args)
        if test $status -eq 0 -a $output
            echo -e (string replace "{}" $output $line)
        end
        set i (math $i + 1)
    end
end

function usage
    echo "git-map: Slice through time, collecting results of command on file"
    echo "git-map [<option>...] <file-to-slice-through> <command-to-run> [<command-arg>...]"
    echo
    echo "Options:"
    echo "  (--format|-f) <format-string>     git log format string with placeholder `{}` for"
    echo "                                    command output"
    echo "  (--version|-v)                    print version information"
    echo "  (--help|-h)                       print this help message"
end

function validate_args -a "file" "command"
    if test -e $file
        if type -q $command
        else
            echo "'$command' cannot be executed"
            return 1
        end
    else
        echo "file '$file' not found"
        return 1
    end
end

argparse -n git-map -x h,v,f "h/help" "v/version" "f/format=" -- $argv

if test ! $status -eq 0
    exit 1
end

if set -q _flag_h
    usage
else if set -q _flag_v
    echo "git-map "$_version
else
    if test (count $argv) -lt 2
        usage
        exit 1
    else
        if set -q _flag_f
            set format_str $_flag_f
        else
            set format_str "::%H|%cI\n{}"
        end

        if validate_args $argv
            map $format_str $argv
        else
            exit 1
        end
    end
end
